      subroutine PolarNite_ocean(indat,kele,pxldat,vza,sfctmp,sh_ocean,
     +                           uniform,testbits,qa_bits,nmtests,confdnc)

c---------------------------------------------------------------------
C!F77 
c
c!Description:
c      Routine for performing clear sky tests over polar water
c      surfaces during nightime hours.
c
c      For nighttime polar ocean the groups are:
c          Group 1: High thick cloud
c                   11 micron bt test (masir11)
c                   6.75 micron bt test 
c                   SST test 
c
c          Group 2: Btdif tests
c                   8-11 micron and 11-12 micron bt tests
c                   11-4 micron bt tests
c                   11 micron spatial variability test
c                   7.3-8.6 test
c
c!Input Parameters:
c indat         Input radiance data
c kele          Pixel number
c pxldat        Array containing reflectance or brightness temperatures
c               for all bands for a single pixel
c vza           viewing zenith angle in degrees
c sfctmp        SST for current pixel
c sh_ocean      Logical variable indicating ocean depths < 50 m or within
c               5 km of shoreline
c uniform       Logical variable indicating uniform conditions in context
c
c!Output Parameters:
c testbits      6-byte array containing bit results
c qa_bits       10-byte array containing qa bit results
c nmtests       Number of tests applied for this pixel
c confdnc       product of all applied individual confidences
c
c!Revision History:
c 06/04 Collection 5  R. Frey:
c Added SST test
c Implemented new 11 um variability test
c Added 8.6-7.3 test
c Implemented new 11-12 um thin cirrus test (J. Key version)
c 10/04 Collection 5  R. Frey:
c Added shallow ocean condition on choice of SST threshold. 
c
c!Team-unique Header:
c
c!References and Credits:
c See Cloud Mask ATBD-MOD-35.
c
c!Design Notes:
c    Externals:
c       Subroutines conf_test,set_bit,clear_bit,set_qa_bit,tview,
c                   chk_spatial2,check_bits
c       Function trispc
c
c!END
c---------------------------------------------------------------------

      implicit none
      save 

      include 'global.inc'
      include 'PolarNite_ocean_thr.inc'
c ...
c ... scalar arguments ..
      real vza,confdnc,sfctmp
      integer nmtests,kele
      logical sh_ocean,uniform
c ...
c ... array arguments ..
      real pxldat(inband),indat(npixel,nlcntx,inband)
      byte testbits(6),qa_bits(10)
c ...
c ... local scalars ..
      real c1,c3,c4,mas11_4,masdf1,Rel_equality_EPS,
     +     masdf2,masir11,masir12,masir4,masir8,
     +     masir65,c6,cmin1,cmin2,groups,
     +     fac,pre_confdnc,tri_thres,c7,diftemp,dfthrsh,schi,
     +     cosvza,dtr,pi,c10,midpt,a,max_vza,corr,sfcdif,
     +     masir73,dwvs,c9,c11,np,sst_thrsh
      integer nptests,rtn,kk,debug,h_output,npix

      parameter(Rel_equality_EPS = 0.000001)
      parameter(max_vza = 65.49)
c ...
c     local arrays
      real hicut,locut
      integer ngtests(2)

c ... external functions ..
      real trispc
      external trispc
c ...
c ... external subroutines ..
      external conf_test,set_bit,clear_bit,set_qa_bit,tview,
     +         chk_spatial2,check_bits

c ... Common statement for debug purposes
      common / bug / debug, h_output

c ... compute degrees-to-radians conversion
      pi = acos(-1.0)
      dtr = pi/180.0
c ... nmtests counts the number of tests applied to this pixel
      nmtests = 0
c ... nptests counts the number of tests passed
      nptests = 0
c     ngtests counts the number of tests applied per group
      ngtests(1) = 0
      ngtests(2) = 0
c ... confidence to 1.0 to begin with
      confdnc = 1.0

c ... place band values into individual variables for easy
c ... identification
      masir4 = pxldat(22)
      masir65 = pxldat(27)
      masir73 = pxldat(28)
      masir8 = pxldat(29)
      masir11 = pxldat(31)
      masir12 = pxldat(32)

c ...
      masdf2 = 0.0
      masdf1 = 0.0
      mas11_4 = 0.0
      dfthrsh = 0.0
      schi = 0.0
      diftemp = 0.0

c ... the c suffix variables represent individual test confidences
      c1 = 0.0
      c3 = 0.0
      c4 = 0.0
      c6 = 0.0
      c7 = 0.0
      c9 = 0.0
      c10 = 0.0
      c11 = 0.0
      cmin1 = 1.0
      cmin2 = 1.0
 

c ... debug statement ............................................
      if (debug .gt. 0) then
        write(h_output,'(10x/,''Processing subroutine PolarNite_ocean '',/)')
      endif
c ................................................................


C     **** GROUP 1 TESTS *************************************
c     11 micron brightness temperature threshold test

      if (nint(masir11) .ne. nint(bad_data)) then
        nmtests = nmtests + 1
        call set_qa_bit(qa_bits,13)
c ...   compare to daytime ocean threshold, set clear bit if passed
        if (masir11 .ge. pnobt11(2)) then
          call set_bit(testbits,13)
          nptests = nptests + 1
        end if
c ...   calculate confidence compared to low and high confidence cutoffs
        call conf_test(masir11,pnobt11(1),pnobt11(3),pnobt11(4),
     *                 pnobt11(2),1,c1)
        cmin1 = min(cmin1,c1)
        ngtests(1) = ngtests(1) + 1
      endif

c ... debug statement ............................................
      if (debug .gt. 0) then
        write(h_output,'(1x,''masir11: '',5f10.2)') masir11,pnobt11(1),
     +          pnobt11(2),pnobt11(3),pnobt11(4)
      endif
c ................................................................


c     H20 vapor channel (6.7 micron) high cloud test
      if (nint(masir65) .ne. nint(bad_data)) then
        nmtests = nmtests + 1
        call set_qa_bit(qa_bits,15)
        if (masir65 .gt. pnoh20(2)) then
          call set_bit(testbits,15)
          nptests = nptests + 1
        end if
        call conf_test(masir65,pnoh20(1),pnoh20(3),pnoh20(4),
     *                pnoh20(2),1,c3)
        cmin1 = min(cmin1,c3)
        ngtests(1) = ngtests(1) + 1
      endif

c ... debug statement ............................................
      if (debug .gt. 0) then
        write(h_output,'(1x,''masir65: '',5f10.2)') masir65,pnoh20(1),
     +          pnoh20(2),pnoh20(3),pnoh20(4)
      endif
c ................................................................

c ... SST test

      if ( (nint(masir11) .ne. nint(bad_data)) .and.
     +     (nint(masir11) .ne. nint(bad_data)) ) then

       if (sfctmp .gt. 0.0 .and. sfctmp .lt. 350.0 ) then

        nmtests = nmtests + 1
        call set_qa_bit(qa_bits,27)

        if(sh_ocean) then
          sst_thrsh = 10.0
        else
          sst_thrsh = 6.0
        end if

        masdf1 = masir11 - masir12
        if(masdf1 .ge. 1.0) then
          midpt = sst_thrsh + (2.0 * nint(masdf1))
        else
          midpt = sst_thrsh
        end if

        a = vza / max_vza
        corr = (a**4) * 3.0
        midpt = midpt + corr
        locut = midpt + 1.0
        hicut = midpt - 2.0

        sfcdif = sfctmp - masir11

        if( sfcdif .lt. midpt ) then
          call set_bit(testbits,27)
          nptests = nptests + 1
        end if

        call conf_test(sfcdif,locut,hicut,1.0,midpt,1,c10)
        cmin1 = min(cmin1,c10)
        ngtests(1) = ngtests(1) + 1

       endif

      endif

c ... debug statement ............................................
      if (debug .gt. 0) then
        write(h_output,'(1x,''sfctmp: '',9f10.3)') sfctmp,masir11,
     +   sfcdif,a,corr,locut,midpt,hicut,c10
      endif
c ................................................................

c     *****  END OF GROUP 1 TESTS  ***************************
 
 
 
c     ****  GROUP 2 TESTS  ***********************************
c ... tri-spectral tests - 8, 11 and 12 micron BTDIF's
c ... calculate 8 minus 11 and 11 minus 12 micron BTDIFs
      if (nint(masir11) .ne. nint(bad_data)
     +                 .and.
     +    nint(masir12) .ne. nint(bad_data)
     +                 .and.
     +    nint(masir8)  .ne. nint(bad_data)) then

        masdf2 = masir8 - masir11
        masdf1 = masir11 - masir12

c       Get clear sky 8-11 micron clear sky thresholds based
c       upon 11-12 difference and compare to global regressions
c       determined from global HIRS data
        tri_thres = trispc(masdf1)
        nmtests = nmtests + 1
        call set_qa_bit(qa_bits,18)
        if (masdf2.lt.tri_thres) then
          nptests = nptests + 1
          call set_bit(testbits,18)
        end if
        locut = tri_thres + .5
        hicut = tri_thres - .5
        call conf_test(masdf2,locut,hicut,1.0,tri_thres,1,c4)
        cmin2 = min(cmin2,c4)
        ngtests(2) = ngtests(2) + 1
      endif

c ... debug statement ............................................
      if (debug .gt. 0) then
        write(h_output,'(1x,''trispec: '',6f10.2)') masdf1,masdf2,
     +          tri_thres,locut,hicut,c4
      endif
c ................................................................


      if (nint(masir11) .ne. nint(bad_data)
     +                 .and.
     +    nint(masir12) .ne. nint(bad_data)
     +                  .and.
     +              vza .gt. 0.0) then

        masdf1 = masir11 - masir12

c ...   11-12um brightness temperature difference test
c ...   for thin cirrus).
c ...   added apollo viewing angle/av4t regressed threshold.
c ...   calculate secant of viewing zenith angle.
        cosvza = cos(vza*dtr)
        if (abs(cosvza).gt.Rel_equality_EPS) then
          schi = 1.0/cosvza
        else
          schi = 99.0
        end if
 
c ...   interpolate look-up table values of 11 - 12 micron bt
c ...   difference thresholds (function of viewing zenith
c ...   and 11 micron brightness temperature).
        call tview(1,schi,masir11,diftemp)
 
c ...   if a threshold was determined by apollo, then use this
c ...   as the thin cirrus test, otherwise use a standard threshold
        if (diftemp.lt.0.1 .or. abs(schi-99.0).lt.0.0001) then
          dfthrsh = pno11_12hi(1)
        else
          dfthrsh = diftemp
        end if
 
c ...   Since the IR BTDIF testbit was already potentially set,
c ...   then only change the bit if the current test failed
        nmtests = nmtests + 1
        call set_qa_bit(qa_bits,18) 
        if (masdf1.le.dfthrsh) then
          nptests = nptests + 1
        else
          call check_bits(testbits,18,rtn)
          if (rtn .eq. 1) then
            call clear_bit(testbits,18)
          end if
        endif
        locut = dfthrsh + (0.3 * dfthrsh)
        hicut = dfthrsh - 1.25
        call conf_test(masdf1,locut,hicut,1.0,dfthrsh,1,c7)
        cmin2 = min(cmin2,c7)
        ngtests(2) = ngtests(2) + 1
      endif

c ... debug statement ............................................
      if (debug .gt. 0) then
        write(h_output,'(1x,''APOLLO masdf1: '',6f10.2)') masdf1,
     +          pno11_12hi(1),dfthrsh,locut,hicut,c7
      endif
c ................................................................


c ... 11 minus 4 micron BTDIF fog and low cloud test.
c ... for now placing in the SWIR bit place holder
      if (nint(masir11) .ne. nint(bad_data)
     +                 .and.
     +    nint(masir4) .ne. nint(bad_data)) then

        nmtests = nmtests + 1
        call set_qa_bit(qa_bits,19)
        mas11_4 = masir11 - masir4
        if (mas11_4 .le. pno11_4lo(2)) then
          call set_bit(testbits,19)
          nptests = nptests + 1
        end if
        call conf_test(mas11_4,pno11_4lo(1),pno11_4lo(3),pno11_4lo(4),
     +                pno11_4lo(2),1,c6)
        cmin2 = min(cmin2,c6)
        ngtests(2) = ngtests(2) + 1
      endif

c ... debug statement ............................................
      if (debug .gt. 0) then
        write(h_output,'(1x,''mas11_4: '',6f10.2)')mas11_4,pno11_4lo(1),
     +          pno11_4lo(2),pno11_4lo(3),pno11_4lo(4),c6
      endif
c ................................................................

c ... Water vapor cloud test (but not over very cold water).

      if( sfctmp .ge. 280.0 ) then

      if( nint(masir73) .ne. nint(bad_data) .and.
     +    nint(masir8) .ne. nint(bad_data)) then

        dwvs = masir8 - masir73

        nmtests = nmtests + 1
        call set_qa_bit(qa_bits,29)

        if ( dwvs .gt. pno86_73(2) ) then
          nptests = nptests + 1
          call set_bit(testbits,29)
        end if

        call conf_test(dwvs,pno86_73(1),pno86_73(3),pno86_73(4),
     +                 pno86_73(2),1,c9)
        cmin2 = min(cmin2,c9)
        ngtests(2) = ngtests(2) + 1

      endif

      endif

c ... debug statement ............................................
      if (debug .gt. 0) then
         write(h_output,'(1x,''dwvs: '',5f6.2)') dwvs,pno86_73(1),
     +      pno86_73(2),pno86_73(3),c9
      endif
c ................................................................

c ... Variability test

      if (uniform) then

        nmtests = nmtests + 1
        call set_qa_bit(qa_bits,30)

        call chk_spatial2(indat,kele,npix)
        np = npix * 1.0

        if ( np .gt. pno_11var(2) ) then
          nptests = nptests + 1
          call set_bit(testbits,30)
        end if

        call conf_test(np,pno_11var(1),pno_11var(3),pno_11var(4),
     +                 pno_11var(2),1,c11)
        cmin2 = min(cmin2,c11)
        ngtests(2) = ngtests(2) + 1

c ..... debug statement ..........................................
        if (debug .gt. 0) then
          write(h_output,'(1x,''var: '',5f10.3)') np,pno_11var(1),
     +          pno_11var(2),pno_11var(3),c11
        endif
c ................................................................

      end if

c *******     END OF GROUP 2 TESTS ****************************
c
c
c     Determine final confidence based on group values
      pre_confdnc = cmin1 * cmin2 
      groups = 0.0
      do kk = 1,2
        if(ngtests(kk) .gt. 0) then
          groups = groups + 1.0
        end if
      enddo
      if (groups .gt. 0) fac = 1.0 / groups
      confdnc = pre_confdnc**fac

c ... debug statement ............................................
      if (debug .gt. 0) then
        write(h_output,'(1x,''tests '',6i10)') nmtests,nptests,ngtests
        write(h_output,'(1x,''confdnc '',8f8.5/,4f8.5)') c1,c3,c4,
     +         c6,c7,c9,c10,c11,cmin1,cmin2,fac,confdnc
      endif
c ................................................................
 
      return
      end
